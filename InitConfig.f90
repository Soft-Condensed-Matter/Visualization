PROGRAM INITCONF
 IMPLICIT NONE
 INTEGER, PARAMETER:: D      = KIND(1.0D0)  !DOUBLE PRECISION
 INTEGER, PARAMETER:: NPARTX = 100000       !MAXIMUM NUMBER OF PARTICLES
 
 REAL(D), PARAMETER:: SIGMA1 = 1.0D0        !SOLVENT DIAMETER
 REAL(D), PARAMETER:: SIGMA2 = 10.0D0       !COLLOID DIAMETER

 INTEGER:: NPART                            !TOTAL NUMBER OF PARTICLES
 INTEGER:: NPARA                            !SOLVENT PARTICLES
 INTEGER:: NPARC                            !COLLOID PARTICLES

 REAL(D):: RHOSTAR                          !SOLVENT NUMBER DENSITY
 REAL(D):: BOXX,BOXY                        !LENGHT SIMULATION BOX
 REAL(D):: RX(NPARTX),RY(NPARTX)            !PARTICLES POSITIONS
 REAL(D):: SIGMA12                          !MIXTURE AVERAGE DIAMETER

!THIS ONLY MUST EXIST IN THE ROUTINE OF INITIAL CONFIGURATION
 INTEGER:: I,K

 REAL(D):: XR(NPARTX),YR(NPARTX)            !AUXILIAR VECTORS TO DEFINE SOLVENT POSITIONS
 REAL(D):: DEL                              !CLOSEST SEPARATION DISTANCE
 REAL(D):: DX,DY,RIJ                        !SEPARATION DISTANCE

 RHOSTAR=0.80D0                             !SOLVENT DENSITY
 NPARA=99999                                !SOME NUMBER OF SOLVENT PARTICLES
 NPARA=INT(DSQRT(DBLE(NPARA)))
 NPARA=NPARA*NPARA                          !SQUARE NUMBER OF PARTICLES 
 
 BOXX=DSQRT(DBLE(NPARA)/RHOSTAR)*SIGMA1     !SIMULATION BOX LENGHT
 BOXY=BOXX                                  !SQUARE SIMULATION BOX
 DEL=DSQRT(1.0D0/RHOSTAR)                   !MEAN SEPARATION DISTANCE 

 XR(1)=(-BOXX + DEL)*0.5D0                  !FIRST SOLVENT PARTICLE POSITION
 YR(1)=(-BOXY + DEL)*0.5D0                  !FIRST SOLVENT PARTICLE POSITION

 DO I=2,NPARA
    XR(I)=XR(I-1) + DEL                     !X POSITION RESPECT TO i-1
    YR(I)=YR(I-1)                           !Y POSITION RESPECT TO i-1
    IF(XR(I) .GT. 0.5D0*BOXX)THEN
      XR(I)=XR(1)
      YR(I)=YR(I-1) + DEL                   !UPWARDS Y POSITION RESPECT TO i-1
    ENDIF
 ENDDO

 K=0                                        !COUNTER FOR FINAL SOLVENT PARTICLES
 NPARC=1                                    !NUMBER OF COLLOIDS
 SIGMA12=0.5D0*(SIGMA1 + SIGMA2)            !AVERAGE SOLVENT-COLLOID DIAMETER

 DO I=1,NPARA
    DX=0.0D0 - XR(I)                        !X DISTANCE BETWEEN SOLVENT AND COLLOID
    DY=0.0D0 - YR(I)                        !Y DISTANCE BETWEEN SOLVENT AND COLLOID

    RIJ=DSQRT(DX*DX + DY*DY)                !SEPARATION DISTANCE
    IF(RIJ .GT. SIGMA12)THEN                !AVOID OVERLAP
      K=K+1                                 !UPDATE THE NUMBER OF PARTICLES NOT OVERLAPING
      RX(K)=XR(I)                           !DEFINE SOLVENT X POSITION
      RY(K)=YR(I)                           !DEFINE SOLVENT Y POSITION
    ENDIF 
 ENDDO

 NPARA=K                                    !FINAL NUMBER OF SOLVENT PARTICLES
 NPART=NPARA + NPARC                        !NUMBER OF PARTICLES

 RX(NPART)=0.0D0                            !COLLOID X POSITION
 RY(NPART)=0.0D0                            !COLLOID Y POSITION

 OPEN(UNIT=10,FILE='IConf.xyz')             !FINAL CONFIGURATION

 WRITE(10,*)NPART
 WRITE(10,*)'FRAME',1
 DO I=1,NPART
    IF(I .LE. NPARA)THEN
      WRITE(10,*)'C',RX(I),RY(I),0.0D0
    ELSE
      WRITE(10,*)'A',RX(I),RY(I),0.0D0
    ENDIF
 ENDDO

 CLOSE(UNIT=10)
 STOP
END PROGRAM INITCONF
